plugins {
    id 'java'
}

wrapper {
    gradleVersion = '8.5'
}

repositories {
    mavenCentral()
}

group = 'com.example.junit5'
version = '1.0-SNAPSHOT'

def allureVersion = '2.25.0'
def aspectJVersion = '1.9.21'
def kotlinVersion = '1.9.21'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

dependencies {
    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation "io.qameta.allure:allure-junit5"

    testImplementation platform("org.junit:junit-bom:5.10.1")
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    testImplementation "org.slf4j:slf4j-simple:2.0.9"
}

test {
    ignoreFailures = true
    useJUnitPlatform()
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
}
